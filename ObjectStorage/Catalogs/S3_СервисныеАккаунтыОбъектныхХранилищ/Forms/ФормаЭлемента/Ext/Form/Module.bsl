
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗагрузитьКлючиИзХранилища(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	СохранитьКлючиВХранилище(Отказ, ТекущийОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторКлючаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	S3_ОбъектноеХранилищеКлиент.ПолеПароляНачалоВыбора(Элемент, ИдентификаторКлюча, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СекретныйКлючНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	S3_ОбъектноеХранилищеКлиент.ПолеПароляНачалоВыбора(Элемент, СекретныйКлюч, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьКлючиИзХранилища(ТекущийОбъект)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключи = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ТекущийОбъект.Ссылка, "ИдентификаторКлюча, СекретныйКлюч");
	
	Если Ключи <> Неопределено Тогда
		ИдентификаторКлюча = Ключи.ИдентификаторКлюча;
		СекретныйКлюч      = Ключи.СекретныйКлюч;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКлючиВХранилище(Отказ, ТекущийОбъект)
	
	Если Не Отказ Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, ИдентификаторКлюча, "ИдентификаторКлюча");
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, СекретныйКлюч, "СекретныйКлюч");
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
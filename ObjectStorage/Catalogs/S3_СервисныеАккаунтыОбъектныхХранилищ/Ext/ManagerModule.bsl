
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция СервисныйАккаунтПоУмолчанию() Экспорт
	Возврат Константы.S3_СервисныйАккаунтПоУмолчанию.Получить();
КонецФункции

Функция ПолучитьНастройкиСервисногоАккаунта(СервисныйАккаунт) Экспорт
	
	НастройкиСервисногоАккаунта = НовыйНастройкиСервисногоАккаунта();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Ссылка КАК СервисныйАккаунт,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Сервер КАК Сервер,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Регион КАК Регион,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.ЗащищенноеСоединение КАК ЗащищенноеСоединение,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.ТипАутентификации КАК ТипАутентификации,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Сервис КАК Сервис,
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Использовать КАК Использовать
	|ИЗ
	|	Справочник.S3_СервисныеАккаунтыОбъектныхХранилищ КАК S3_СервисныеАккаунтыОбъектныхХранилищ
	|ГДЕ
	|	S3_СервисныеАккаунтыОбъектныхХранилищ.Ссылка = &Ссылка
	|	И НЕ S3_СервисныеАккаунтыОбъектныхХранилищ.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Ссылка", СервисныйАккаунт);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(НастройкиСервисногоАккаунта, Выборка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НастройкиСервисногоАккаунта.СервисныйАккаунт) Тогда
		
		Ключи = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			НастройкиСервисногоАккаунта.СервисныйАккаунт,
			"ИдентификаторКлюча, СекретныйКлюч");
		
		Если Ключи <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НастройкиСервисногоАккаунта, Ключи);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НастройкиСервисногоАккаунта;
	
КонецФункции

Функция НовыйНастройкиСервисногоАккаунта()
	
	НастройкиСервисногоАккаунта = Новый Структура;
	НастройкиСервисногоАккаунта.Вставить("СервисныйАккаунт");
	НастройкиСервисногоАккаунта.Вставить("Сервер");
	НастройкиСервисногоАккаунта.Вставить("Регион");
	НастройкиСервисногоАккаунта.Вставить("ЗащищенноеСоединение");
	НастройкиСервисногоАккаунта.Вставить("ТипАутентификации");
	НастройкиСервисногоАккаунта.Вставить("Сервис");
	НастройкиСервисногоАккаунта.Вставить("Использовать");
	НастройкиСервисногоАккаунта.Вставить("ИдентификаторКлюча");
	НастройкиСервисногоАккаунта.Вставить("СекретныйКлюч");
	
	Возврат НастройкиСервисногоАккаунта;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли